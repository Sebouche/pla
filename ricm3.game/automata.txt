Player(Surface){
	* (Surface):
	| Key(z) & Cell(N,G,1) ? Jump(F) :(Underground)
	| Key(w) & !Cell(F,O,64) / Cell(F,T,64) ? Wizz(F) :(Surface)
	| Key(p) & Cell(F,T,1) ? Pop(F) :(InsideTurret)
	| Key(z) & !Cell(N,O,1) ? Move(N) :(Surface)
	| Key(q) & !Cell(W,O,1) ? Move(W) :(Surface)
	| Key(s) & !Cell(S,O,1) ? Move(S) :(Surface)
	| Key(d) & !Cell(E,O,1) ? Move(E) :(Surface)
	| Key(m) ? Throw(F) :(BuyMenu)
	* (BuyMenu):
	| Key(m) ? Throw(F) :(Surface)
	| Key(ENTER) ? Throw(F) :(Surface)
	* (Underground):
	| True ? Jump(F) :(Surface)
	| Key(z) & Cell(N,V,1) ? Move(N) :(Jumping)
	| Key(w) & Cell(F,V,64) ? Wizz(F) :(Underground)
	| Key(q) & Cell(W,J,1) ? Move(W) :(Ladder)
	| Key(d) & Cell(E,J,1) ? Move(E) :(Ladder)
	| Key(q) & Cell(W,V,1) ? Move(W) :(Underground)
	| Key(d) & Cell(E,V,1) ? Move(E) :(Underground)
	| Key(m) ? Throw(F) :(Menu1)
	| Cell(S,V,1) ? Move(S) :(Underground)
	* (Jumping):
	| Cell(S,O,1) ? Protect() :(Underground)
	| Key(q) & Cell(W,V,1) ? Move(W) :(Underground)
	| Key(d) & Cell(E,V,1) ? Move(E) :(Underground)
	| Key(m) ? Throw(F) :(Menu2)
	| True ? Move(S) :(Underground)
	* (Ladder):
	| Key(z) & Cell(N,G,1) ? Jump(F) :(Surface)
	| Key(z) & Cell(N,J,1) ? Move(N) :(Ladder)
	| Key(s) & Cell(S,J,1) ? Move(S) :(Ladder)
	| Key(z) & Cell(N,V,1) ? Move(N) :(Underground)
	| Key(s) & Cell(S,V,1) ? Move(S) :(Underground)
	| Key(q) & Cell(W,V,1) ? Move(W) :(Underground)
	| Key(d) & Cell(E,V,1) ? Move(E) :(Underground)
	| Key(m) ? Throw(F) :(Menu3)
	* (Menu1):
	| Key(m) ? Throw(F) :(Underground)
	| Key(ENTER) ? Throw(F) :(Underground)
	* (Menu2):
	| Key(m) ? Throw(F) :(Jumping)
	| Key(ENTER) ? Throw(F) :(Jumping)
	* (Menu3):
	| Key(m) ? Throw(F) :(Ladder)
	| Key(ENTER) ? Throw(F) :(Ladder)
	* (InsideTurret):
	| Key(ENTER) ? Pop(F) :(Surface)
	* ()
}

Bat(E1){
	* (E1):
	| !GotPower() ? :()
	| True ? Pop() :(E2)
	* (E2):
	| !GotPower() ? :() 
	| Cell(F,V) ? Move(F) :(E1)
	| Cell(F,@) ? Hit() :(E1)
	| True ? Pop() :(E2)
	* ()
}

Mole(E1){
	* (E1): True ? Pop() :(E2)
	* (E2):
	| Cell(S,V,1) ? Move(S) :(E1)
	| Cell(F,V) ? Wizz() :(E1)
	| True ? Move(F) :(E2)
}

Mouse(E3){
	* (E1):
	| !GotPower() ? :()
	| Cell(F,O,1) ? Hit() :(E2)
	| Cell(F,A,1) ? Hit() :(E3)
	| True ? Egg() :(E4)
	* (E2):
	| !GotPower() ? :()
	| Cell(F,O,1) ? Hit() :(E2)
	| Cell(F,V) ? Move(F) / Egg() :(E4)
	* (E3):
	| !GotPower() ? :()
	| Cell(F,@,1) ? Hit() :(E3)
	| True ? Pop() :(E1)
	* (E4):
	| !GotPower() ? :()
	| Cell(F,@,1) ? Hit() :(E1)
	| !Cell(F,@,1) ? Pop() :(E1)
	* ()
}

Dog(E2){
	* (E1):
	| !GotPower() ? :()
	| Cell(F,O,1) ? Hit() :(E3)
	| Cell(F,@,1) / Cell(F,T,1) ? Hit() :(E4)
	| !Cell(F,O,1) ? Move(F) :(E2)
	* (E2):
	| !GotPower() ? :()
	| True ? Pop() :(E1)
	* (E3):
	| !GotPower() ? :()
	| Cell(F,O,1) ? Hit() :(E3)
	| True ? Move(F) :(E2)
	* (E4):
	| Cell(F,@,1) / Cell(F,T,1) ? Hit() :(E4)
	| True ? Move(F) :(E2)
	* ()
}

Rabbit(E1){
	* (E1):
	| !GotPower() ? :()
	| Cell(F,O,1) / Cell(F,V,1) ? Move(F) :(E2)
	| Cell(F,T,1) ? Hit() :(E1)
	* (E2):
	| !GotPower() ? :()
	| True ? Pop() :(E1)
	* ()
}

Turtle(E2){
	* (E1):
	| !GotPower() ? Kamikaze :()
	| Cell(F,O,1) ? Hit() :(E1)
	| Cell(F,T,1) ? Hit() :(E1)
	| !Cell(F,O,1) ? Move(F) :(E2)
	* (E2):
	| !GotPower() ? Kamikaze :()
	| True ? Pop() :(E1)
	* ()
}

Tesla(E1){
	* (E1):
	| !GotPower() ? :()
	| True ? Pop() :(E2)
	* (E2):
	| !GotPower() ? :()
	| True ? Hit(F) :(E1)
	* ()
}

Block(Init){
	* (Init): 
	| !GotPower() ? Pop() :()
	| True ? Wizz() :(Init)
	* ()
}
	
Barbed(Init){
    * (Init): 
    | !GotPower() ? Wizz() :()
	| True ? Pop() :(Init)
	* ()
}

Wall(Init){
	* (Init): 
    | !GotPower() ? Pop() :()
	| True ? Wizz() :(Init)
	* ()
}

Spawner(Init){
	* (Init): 
	| True ? Egg() :(Init)
	* ()
}

Water(Init){
	* (Init): 
	| True ? Egg() :(Init)
	* ()
}

Moving(Init){
	* (Init):
	| Key(z) & !Cell(N,O,1) ? Move(N) :(Init)
	| Key(q) & !Cell(W,O,1) ? Move(W) :(Init)
	| Key(s) & !Cell(S,O,1) ? Move(S) :(Init)
	| Key(d) & !Cell(E,O,1) ? Move(E) :(Init)
	* ()
}
